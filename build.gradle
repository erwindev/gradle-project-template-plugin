/*
* Plugins
*/
plugins{
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

/*
* Dependencies
*/
repositories {
    jcenter()
}

dependencies {
    compile     gradleApi()
    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
}

/*
* Project Configuration
*/

project.description     = 'ustom Gradle Plugin for Scaffolding Project'
project.group           = 'com.erwindev'

project.ext.name        = 'template-project'
project.ext.pluginId    = 'gradle-project-template-plugin'
project.ext.scm         = 'https://github.com/erwindev/gradle-project-template-plugin.git'
project.ext.url         = 'https://github.com/erwindev/gradle-project-template-plugin'

/*
* Configuration for: java-gradle-plugin
* Register the entry point
*/

gradlePlugin {
    plugins {
        scaffoldPlugin {
            id = project.pluginId
            implementationClass = 'com.erwin.template.TemplatePlugin'
        }
    }
}

pluginBundle {
    website = project.url
    vcsUrl = project.scm
    tags = ['template', 'scaffold']

    plugins {
        scaffoldPlugin {
            displayName = 'Scaffolding Gradle Plugin'
            description = project.description
            id          = project.pluginId
        }
    }
}

/*
* Publishing to local maven repo
*/

publishing {
    publications {
        pluginPublication (MavenPublication) {
            from        components.java
            groupId     project.group
            artifactId  project.name
            version     project.version
        }
    }
}

bintray{
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'erwindev-maven-repo'
        name = project.name
        userOrg = 'erwindev'
        licenses = ['Apache-2.0']
        vcsUrl = project.ext.scm
        websiteUrl = project.ext.url
        githubRepo = 'erwindev/gradle-project-template-plugin'
        githubReleaseNotesFile = 'README.md'
    }
}